/*
* When using plugins in gradle, we need to tell it where it can download them:
*/
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.junit.platform:junit-platform-gradle-plugin:1.0.0-M4'
    }
}

/*
* The most basic setting which apply to ALL projects (including the root) go here:
*/
allprojects{
	apply plugin: 'eclipse'
	apply plugin: 'java'
	
	group = projectGroup
	version = projectVersion

	repositories {
		jcenter()
	}
}

/*
* Shared dependencies between ALL subprojects go in here:
*/
subprojects {
	apply plugin: 'org.junit.platform.gradle.plugin'

	dependencies {
		testCompile("org.junit.jupiter:junit-jupiter-api:5.0.0-M4")
   		testRuntime("org.junit.jupiter:junit-jupiter-engine:5.0.0-M4")
	}
}

/*
* Provide a fixed version of gradle for people building this project.
*/
task wrapper(type: Wrapper) {
	gradleVersion = gradleWrapperVersion
}

/*
* Load private and secret properties!
*/
def localProps = project.rootProject.file('local.properties')
if (localProps.exists())
{
	Properties properties = new Properties()
	properties.load(localProps.newDataInputStream())
	ext.localSecretKeystorePassword = properties.getProperty('localSecretKeystorePassword')
	ext.localSecretKeyPassword = properties.getProperty('localSecretKeyPassword')
}
else
{
	//for the travis build we set the secrets via enviraonment variables, because these can be encrypted with travis api
	ext.localSecretKeystorePassword = System.getenv("KEYSTORE_PW")
	ext.localSecretKeyPassword = System.getenv("KEY_PW")
}
